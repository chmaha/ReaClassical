NoIndex: true
desc: RCModulator (chmaha)
author: chmaha
about:
  Add after 100% wet static convolution reverb to give some movement to the tail
  JSFX released under GPLv3 license

slider1:0.12<0,1,0.01>LFO Frequency 1 (Hz)
slider2:0.31<0,1,0.01>LFO Frequency 2 (Hz)
slider3:25<0,50,1>Depth (%)
slider4:0<0,1,1{On,Off}>Subtle Stereo movement

@init
twoPi = 2*$pi;

drift_amt1 = 0.01;
drift_amt2 = 0.02;

drift_interval = srate * 0.5; // every 0.5s
drift_counter = 0;

ph1 = 0;
ph2 = 0;

target_freq1 = freq1 = slider1;
target_freq2 = freq2 = slider2;

@slider
freq1_base = slider1;
freq2_base = slider2;
depth = slider3/100;
phase = slider4;

@block
drift_counter += samplesblock;

(drift_counter >= drift_interval) ? (
    drift_counter = 0;
    target_freq1 = freq1_base * (1 + (rand(2)-1) * drift_amt1);
    target_freq2 = freq2_base * (1 + (rand(2)-1) * drift_amt2);
);

smoothing = 0.1;
freq1 += (target_freq1 - freq1) * smoothing;
freq2 += (target_freq2 - freq2) * smoothing;

incr1 = twoPi*freq1/srate;
incr2 = twoPi*freq2/srate;

@sample
ph1 += incr1;
ph2 += incr2;

ph1 >= twoPi ? ph1 -= twoPi;
ph2 >= twoPi ? ph2 -= twoPi;

lfo1 = sin(ph1);
lfo2 = sin(ph2);

lfo_left  = (lfo1 + lfo2) * 0.5;
(phase == 0) ? lfo_right = (lfo1 + sin(ph2 + $pi/2)) * 0.5 // 90Â° phase offset for right channel
: lfo_right = (lfo1 + lfo2) * 0.5;

gain_left  = 1 + lfo_left  * depth;
gain_right = 1 + lfo_right * depth;

spl0 *= gain_left;
spl1 *= gain_right;
