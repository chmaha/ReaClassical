version: 1.0
desc:Primal Overtones Waveshaper (chmaha)
author: chmaha
about:
  Uses DSP from airwindows "ShortBuss" by Chris Johnson
  https://github.com/airwindows/airwindows/tree/master/plugins/LinuxVST/src/ShortBuss (MIT License)
  JSFX version released under GPLv3 license

slider1:0<-1,1,0.01>Effect
slider2:1<0,1,0.01>Dry/Wet

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init


@slider

A = slider1;
B = slider2;

@block
overallscale = 1.0;
overallscale /= 44100.0;
overallscale *= srate;

sbScale = (pow(A,3)*0.001)/sqrt(overallscale);
wet = B;

@sample
inputSampleL = spl0;
inputSampleR = spl1;

drySampleL = inputSampleL;
drySampleR = inputSampleR;

inputSampleL += (2.0 * inputSampleL * inputSampleL) * sbSampleL;
sbSampleL += ((inputSampleL - sin(inputSampleL))*sbScale);
sbSampleL = sin(sbSampleL*0.015625)*64.0;

inputSampleR += (2.0 * inputSampleR * inputSampleR) * sbSampleR;
sbSampleR += ((inputSampleR - sin(inputSampleR))*sbScale);
sbSampleR = sin(sbSampleR*0.015625)*64.0;

(wet !=1.0) ? (
	inputSampleL = (inputSampleL * wet) + (drySampleL * (1.0-wet));
	inputSampleR = (inputSampleR * wet) + (drySampleR * (1.0-wet));
);

spl0 = inputSampleL;
spl1 = inputSampleR;
