Version: 1.1
desc: Magnetica Tape Emulation (chmaha)
author: chmaha
about:
  Uses DSP from airwindows "TapeHack" by Chris Johnson
  https://github.com/airwindows/airwindows/tree/master/plugins/LinuxVST/src/TapeHack (MIT License)
  JSFX version released under GPLv3 license

slider1:0.1<0,1,0.01>Input
slider2:1<0,1,0.01>Output
slider3:1<0,1,0.01>Dry/Wet

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init

@slider

A = slider1;
B = slider2;
C = slider3;

@block
inputGain = A*10.0;
outputGain = B*0.9239;
wet = C;

@sample
inputSampleL = spl0;
inputSampleR = spl1;

drySampleL = inputSampleL;
drySampleR = inputSampleR;

inputSampleL = max(min(inputSampleL*inputGain,2.305929007734908),-2.305929007734908);
addtwo = inputSampleL * inputSampleL;
empower = inputSampleL * addtwo;
inputSampleL -= (empower / 6.0);
empower *= addtwo;
inputSampleL += (empower / 69.0);
empower *= addtwo;
inputSampleL -= (empower / 2530.08);
empower *= addtwo;
inputSampleL += (empower / 224985.6);
empower *= addtwo;
inputSampleL -= (empower / 9979200.0);

inputSampleR = max(min(inputSampleR*inputGain,2.305929007734908),-2.305929007734908);
addtwo = inputSampleR * inputSampleR;
empower = inputSampleR * addtwo;
inputSampleR -= (empower / 6.0);
empower *= addtwo;
inputSampleR += (empower / 69.0);
empower *= addtwo;
inputSampleR -= (empower / 2530.08);
empower *= addtwo;
inputSampleR += (empower / 224985.6);
empower *= addtwo;
inputSampleR -= (empower / 9979200.0);

inputSampleL = (inputSampleL * outputGain * wet) + (drySampleL * (1.0-wet));
inputSampleR = (inputSampleR * outputGain * wet) + (drySampleR * (1.0-wet));

spl0 = inputSampleL;
spl1 = inputSampleR;
