Version: 1.0
desc:Essenza Dither (chmaha)
author: chmaha
about:
  Uses DSP from airwindows "RawTimbers" and "RawGlitters" by Chris Johnson
  https://github.com/airwindows/airwindows/tree/master/plugins/LinuxVST/src/RawTimbers (MIT License)
  https://github.com/airwindows/airwindows/tree/master/plugins/LinuxVST/src/RawGlitters (MIT License)
  JSFX version released under GPLv3 license

slider1:0<0,1,1{Sotto,Sopra>16-bit Dither Type

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init

@slider

@block

@sample
inputSampleL = spl0;
inputSampleR = spl1;

inputSampleL *= 32768.0;
inputSampleR *= 32768.0;

inputSampleL += 0.381966011250105;
inputSampleR += 0.381966011250105;

(slider1 == 0) ? (

((lastSampleL+lastSampleL) >= (inputSampleL+lastSample2L)) ? outputSampleL = floor(lastSampleL)
: outputSampleL = floor(lastSampleL+1.0);
((lastSampleR+lastSampleR) >= (inputSampleR+lastSample2R)) ? outputSampleR = floor(lastSampleR)
: outputSampleR = floor(lastSampleR+1.0);

) : (slider1 == 1) ? (

((lastSampleL+lastSampleL) <= (inputSampleL+lastSample2L)) ? outputSampleL = floor(lastSampleL)
: outputSampleL = floor(lastSampleL+1.0);
((lastSampleR+lastSampleR) <= (inputSampleR+lastSample2R)) ? outputSampleR = floor(lastSampleR)
: outputSampleR = floor(lastSampleR+1.0);

);

lastSample2L = lastSampleL;
lastSampleL = inputSampleL;

lastSample2R = lastSampleR;
lastSampleR = inputSampleR;

outputSampleL /= 32768.0;
outputSampleR /= 32768.0;

spl0 = outputSampleL;
spl1 = outputSampleR;

