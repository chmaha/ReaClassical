NoIndex: true
desc: Horizon Console Channel (chmaha)
author: chmaha
about:
  Uses DSP from airwindows "Console9Channel" by Chris Johnson
  https://github.com/airwindows/airwindows/tree/master/plugins/LinuxVST/src/Console9Channel (MIT License)
  JSFX version released under GPLv3 license

slider1:0.5<0,1,0.01>Pan
slider2:0.5<0,1,0.01>Fader

in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init

panA = 0.5; panB = 0.5;
inTrimA = 0.5; inTrimB = 0.5;

@slider

A = slider1;
B = slider2;

@block

sample_index = 0;
panA = panB; panB = A*1.57079633;
inTrimA = inTrimB; inTrimB = B*2.0;

@sample
inputSampleL = spl0;
inputSampleR = spl1;

sample_index += 1;
samples_left = samplesblock - sample_index;
temp = samples_left/samplesblock;
gainR = (panA*temp)+(panB*(1.0-temp));
gainL = 1.57079633-gainR;
gainR = sin(gainR); gainL = sin(gainL);

gain = (inTrimA*temp)+(inTrimB*(1.0-temp));
(gain > 1.0) ? gain *= gain;
(gain < 1.0) ? gain = 1.0-pow(1.0-gain,2);
gain *= 0.763932022500211;

inputSampleL = inputSampleL * gainL * gain;
inputSampleR = inputSampleR * gainR * gain;

inputSampleL *= 0.618033988749895;
(inputSampleL > 1.0) ? inputSampleL = 1.0
: (inputSampleL > 0.0) ? inputSampleL = -(exp(log(1 - inputSampleL) * 1.618033988749895) - 1);
(inputSampleL < -1.0) ? inputSampleL = -1.0
: (inputSampleL < 0.0) ? inputSampleL = exp(log(1 + inputSampleL) * 1.618033988749895) - 1;

inputSampleR *= 0.618033988749895;
(inputSampleR > 1.0) ? inputSampleR = 1.0
: (inputSampleR > 0.0) ? inputSampleR = -(exp(log(1 - inputSampleR) * 1.618033988749895) - 1);
(inputSampleR < -1.0) ? inputSampleR = -1.0
: (inputSampleR < 0.0) ? inputSampleR = exp(log(1 + inputSampleR) * 1.618033988749895) - 1;

spl0 = inputSampleL;
spl1 = inputSampleR;
